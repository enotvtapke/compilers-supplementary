-- Stack machine.

import Ref;
import Array;
import List;
import Fun;
import Collection;
import World;
import State;
import Parser;
import Lexer;
import Expr;
import Builtins;
import Buffer;

-- Stack code printer. Takes a list of SM instructions, return its
-- string representation.
public fun showSMInsn (i) {
  -- Shows a location (a reference to function argument, local or global variable)
  fun showLoc (d) {
    fun varity (f) {
      if f then "var" else "val" fi
    }
    
    case d of
      Arg (i) -> sprintf ("arg[%d]", i)
    | Loc (i) -> sprintf ("loc[%d]", i)
    | Glb (x) -> sprintf ("%s", x)
    esac
  }
  
  case i of
    BINOP  (s)       -> sprintf ("BINOP %s", s)
  | LD     (x)       -> sprintf ("LD %s", showLoc (x))
  | LDA    (x)       -> sprintf ("LDA %s", showLoc (x))
  | ST     (x)       -> sprintf ("ST %s", showLoc (x))
  | STI              -> "STI"
  | SEXP   (s, n)    -> sprintf ("SEXP ""%s"", %d", s, n)
  | CONST  (n)       -> sprintf ("CONST %d", n)
  | LABEL  (s)       -> sprintf ("LABEL %s", s)
  | JMP    (l)       -> sprintf ("JMP %s", l)
  | CJMP   (c, l)    -> sprintf ("CJMP %s, %s", c, l)
  | CALL   (f, n)    -> sprintf ("CALL %s, %d", f, n)
  | BEGIN  (f, a, l) -> sprintf ("BEGIN %s, %d, %d", f, a, l)
  | GLOBAL (x)       -> sprintf ("GLOBAL %s", x)
  | END              -> sprintf ("END")
  | DUP              -> "DUP"
  | DROP             -> "DROP"
  | STRING (s)       -> sprintf ("STRING ""%s""", s)
  | ARRAY  (n)       -> sprintf ("ARRAY %d", n)
  | STA              -> "STA"
  | ELEM             -> "ELEM"
  | BUILTIN (f, n)   -> sprintf ("BUILTIN %s, %d", f, n)
  esac
}

public fun showSM (prg) {
  map (fun (i) {showSMInsn (i) ++ "\n"}, prg).stringcat
}

-- Evaluation environment: keeps a mapping between labels and (sub)programs
fun initEvalEnv (insns) {
  var map =
    fix (fun (rec) {
          fun ([m, insns]) {
            case insns of
              {}               -> m
            | LABEL (lab) : tl -> rec ([addMap (m, lab, insns), tl])
            | _ : tl           -> rec ([m, tl])
            esac
          }
        }) $ [emptyMap (compare), insns];
  
  [fun (l) {
     case findMap (map, l) of Some (insns) -> insns esac
  }]
}

-- Accessor function
fun fromLabel (env, lab) {
  env [0] (lab)
}

-- Stack machine interpreter. Takes an environment, a world and a program,
-- returns a final output
fun eval (env, w, insns) {
  -- Global state maps names of global variables to values
  var globalState = ref (fun (x) {error (sprintf ("name ""%s"" is undefined", x), getLoc (x))});
  
  -- Make a fresh local state: a pair of arrays for arguments and local variables;
  -- takes the numbers of arguments and local variables respectively
  fun makeState (a, l) {
    [initArray (a, fun (_) {0}), initArray (l, fun (_) {0})]
  }

  -- Lookups a location in local/global states
  fun lookup ([args, locs], loc) {
    case loc of
      Arg (i) -> args[i]
    | Loc (i) -> locs[i]
    | Loc (i) -> locs[i]
    | Glb (x) -> deref (globalState) (x)
    esac 
  }

  -- Assigns a value to a location
  fun assign ([args, locs], loc, vl) {
    case loc of
      Arg (i) -> args[i] := vl
    | Loc (i) -> locs[i] := vl
    | Glb (x) -> var g = deref (globalState);
                 globalState ::= fun (y) {if compare (x, y) == 0 then vl else g (y) fi}
    esac
  }

  -- Takes n positions from the list, retursn a pair: the remaining list and the taken
  -- sublist
  fun take (list, n) {
    fun inner (n, acc, list) {
      if n == 0
      then [list, acc]
      else inner (n-1, list.hd : acc, list.tl)
      fi
    }

    inner (n, {}, list)
  }

  -- Core interpreter: takes a configuration and a program, returns a configuration
  fun eval(c@[st, cst, s, w], insns) {
    case insns of
        { }       -> c
      | i : insns ->
      case i of
          READ            -> case readWorld(w) of [n, w] -> eval([n:st, cst, s, w], insns) esac
        | WRITE           -> case st of n : st -> eval([st, cst, s, writeWorld(n, w)], insns) esac
        | BINOP (op)      -> case st of y : x : st -> eval([evalOp(op, x, y):st, cst, s, w], insns) esac
        | CONST (n)       -> eval([n:st, cst, s, w], insns)
        | LD (x)          -> eval([lookup(s, x):st, cst, s, w], insns)
        | ST (x)          -> case st of v : st -> assign(s, x, v); eval([st, cst, s, w], insns) esac
        | JMP (l)         -> eval(c, fromLabel(env, l))
        | CJMP (cond, l)  ->
          case st of n : st ->
            if case cond of
                "ne" -> n
              | "e"  -> n == false
            esac then eval([st, cst, s, w], fromLabel(env, l)) else eval([st, cst, s, w], insns) fi
          esac
        | LABEL (l)       -> eval(c, insns)
        | DROP            -> case st of _ : st -> eval([st, cst, s, w], insns) esac
        | LDA (x)         -> eval([Ref(x):st, cst, s, w], insns)
        | STI             -> case st of v : Ref(x) : st -> (*printf("!STI1 %s\n", s.string);*)assign(s, x, v); (* printf("!STI2 %s\n", s.string);*)eval([v:st, cst, s, w], insns) esac
        | GLOBAL (_)      -> eval([st, cst, s, w], insns)
        | CALL (f, n)     -> eval([st, [s, insns]:cst, makeState(n, 0), w], fromLabel(env, f))
        | END             -> case cst of { } -> c | [s, insns] : cst -> eval([st, cst, s, w], insns) esac
        | BEGIN (_, a, l) -> case makeState(a, l) of s ->
          case take(st, a) of
            [st, args] ->
              args := listArray(args);
              iteriArray(fun (i, arg) { assign(s, Arg(i), arg) }, args);
              eval([st, cst, s, w], insns)
          esac
        esac
        | BUILTIN (f, n)  -> eval(evalBuiltin(f, n, c), insns)
        | ARRAY (n)       ->
          case take(st, n) of
            [st, a] -> eval([listArray(a):st, cst, s, w], insns)
          esac
        | STRING(x)       -> eval([x:st, cst, s, w], insns)
        | ELEM            ->
          case st of
            i : Sexp(_, a) : st -> eval([a[i]:st, cst, s, w], insns) |
            i : a : st          -> eval([a[i]:st, cst, s, w], insns)
          esac
        | STA             ->
          case st of
              v : Ref(x) : Ref(_) : st  -> assign(s, x, v); eval([v:st, cst, s, w], insns)
            | v : i : arr : st -> arr[i] := v; eval([v:st, cst, s, w], insns)
          esac
        | SEXP(name, n)   ->
          case take(st, n) of
            [st, a] -> eval([Sexp(name, listArray(a)):st, cst, s, w], insns)
          esac
      esac
    esac
  }
  
  
  eval ([{}, {}, makeState (0, 0), w], insns) [3].getOutput
}

fun evalBuiltin(fName, nArgs, c@[st, cst, s, w]) {
  case fName of
    "$read"   -> case readWorld(w) of [n, w] -> [n:st, cst, s, w] esac |
    "$write"  -> case st of n : st -> [None:st, cst, s, writeWorld(n, w)] esac |
    "$length" -> case st of
      a : st ->
        case a of
          Sexp(_, a) -> [length(a):st, cst, s, w] |
          _          -> [length(a):st, cst, s, w]
        esac
    esac
  esac
}

-- Runs a stack machine for a given input and a given program, returns an output
public fun evalSM (input, insns) {
  eval (initEvalEnv (insns), createWorld (input), insns)
}

-- Compilation environment: generates labels
fun makeCompEnv (nLabels, scopeDepth, state, nLocals, nArgs, functions) {
  -- Generates a new label
  fun genLabel () {
    [sprintf ("L%d", nLabels), makeCompEnv (nLabels+1, scopeDepth, state, nLocals, nArgs, functions)]
  }

  -- Adds a new function 
  fun rememberFun (fLabel, args, body) {
    makeCompEnv (nLabels,
                 scopeDepth,
                 state,
                 nLocals,
                 nArgs,
                 Fun (fLabel, args, body, state) : functions)
  }

  -- Enters a local scope
  fun beginScope () {
    makeCompEnv (nLabels, scopeDepth+1, enterScope (state), nLocals, nArgs, functions)
  }

  -- Leaves a local scope
  fun endScope () {
    makeCompEnv (nLabels, scopeDepth-1, leaveScope (state), nLocals, nArgs, functions)
  }

  -- Adds an argument for the current function
  fun addArg (name) {
    makeCompEnv (nLabels, scopeDepth, addName (state, name, Arg (nArgs)), nLocals, nArgs+1, functions)
  }

  -- Adds a variable in the scope
  fun addVar (name) {
    if isGlobal ()
    then makeCompEnv (nLabels, scopeDepth, addName (state, name, Glb (name, true)), nLocals, nArgs, functions)
    else makeCompEnv (nLabels, scopeDepth, addName (state, name, Loc (nLocals, true)), nLocals+1, nArgs, functions)
    fi
  }

  -- Adds a value in the scope
  fun addVal (name) {
    if isGlobal ()
    then makeCompEnv (nLabels, scopeDepth, addName (state, name, Glb (name, false)), nLocals, nArgs, functions)
    else makeCompEnv (nLabels, scopeDepth, addName (state, name, Loc (nLocals, false)), nLocals+1, nArgs, functions)
    fi
  }

  -- Adds a binding for a function
  fun addFun (name, fLabel, nargs) {
    makeCompEnv (nLabels, scopeDepth, addName (state, name, Fun (fLabel, nargs)), nLocals, nArgs, functions)
  }
  
  -- Enters a function
  fun beginFun (state) {
    makeCompEnv (nLabels, 2, enterFunction (state), 0, 0, functions)
  }

  -- Get functions and empty functions list
  fun getFuns () {
    [functions, makeCompEnv (nLabels, scopeDepth, state, nArgs, nLocals, {})]
  }

  -- Lookups variable
  fun lookupVar (name) {
    case lookup (state, name) of
      Loc (i, true) -> Loc (i)
    | Glb (x, true) -> Glb (x)
    | Arg (i)       -> Arg (i)
    | _             -> error (sprintf ("the name ""%s"" does not designate a variable", name), getLoc (name))
    esac
  }

  -- Lookups value
  fun lookupVal (name) {
    case lookup (state, name) of
      Loc (i, _) -> Loc (i)
    | Glb (x, _) -> Glb (x)
    | Arg (i)    -> Arg (i)
    | _          -> error (sprintf ("the name ""%s"" does not designate a value", name), getLoc (name))
    esac
  }
  
  -- Lookups a name of a function
  fun lookupFun (name) {
    case lookup (state, name) of
      x@Fun (_, _) -> x
    | _            -> error (sprintf ("the name ""%s"" does not designate a function", name), getLoc (name))
    esac
  }

  -- Gets the number of local variables
  fun getLocals () {
    nLocals
  }

  -- Generates an intrinsic function name
  fun genFunLabel (name) {
     if isGlobal ()
     then [sprintf ("L%s", name),             makeCompEnv (nLabels  , scopeDepth, state, nLocals, nArgs, functions)]
     else [sprintf ("L%s_%d", name, nLabels), makeCompEnv (nLabels+1, scopeDepth, state, nLocals, nArgs, functions)]
     fi 
  }

  -- Checks if the current scope is a global
  fun isGlobal () {
    scopeDepth == 2
  }
  
  [genLabel,
   rememberFun,
   beginScope,
   endScope,
   addArg,
   addVar,
   addVal,
   beginFun,
   getFuns,
   lookupVar,
   lookupVal,
   lookupFun,
   getLocals,
   genFunLabel,
   addFun,
   isGlobal
  ]
}

-- Initialize a new compilation environment
fun initCompEnv () {
  makeCompEnv (0, 0, emptyState (), 0, 0, {})
}

-- Accessor functions
fun genLabel (env) {
  env [0] ()
}

fun rememberFun (env, name, args, body) {
  env [1] (name, args, body)
}

fun beginScope (env) {
  env [2] ()
}

fun endScope (env) {
  env [3] ()
}

fun addArg (env, name) {
  env [4] (name)
}

fun addVar (env, name) {
  env [5] (name)
}

fun addVal (env, name) {
  env [6] (name)
}

fun beginFun (env, state) {
  env [7] (state)
}

fun getFuns (env) {
  env [8] ()
}

fun lookupVar (env, name) {
  env [9] (name)
}

fun lookupVal (env, name) {
  env [10] (name)
}

fun lookupFun (env, name) {
  env [11] (name)
}

fun getLocals (env) {
  env [12] ()
}

fun genFunLabel (env, name) {
  env [13] (name)
}

fun addFun (env, name, fLabel, nargs) {
  env [14] (name, fLabel, nargs)
}

fun isGlobal (env) {
  env [15] ()
}

-- Helper function: generates n labels
fun genLabels (env, n) {
  fun inner (env, n) {
    if n == 0
    then {env}
    else case genLabel (env) of
           [lab, env] -> lab : inner (env, n-1)
         esac
    fi
  }

  listArray (inner (env, n))
}

-- Helper function: adds a bunch of arguments
fun addArgs (env, names) {
  foldl (fun (env, name) {addArg (env, name)}, env, names)
}

-- Helper function: adds a bunch of local variables
fun addVars (env, names) {
  foldl (fun (env, name) {addVar (env, name)}, env, names)
}

-- Helper function: adds a bunch of locals values
fun addVals (env, names) {
  foldl (fun (env, name) {addVal (env, name)}, env, names)
}

fun rememberFuns(env, defs) {
  foldl(fun (env, def) {
    case def of
        Fun(name, args, body) ->
          case lookupFun(env, name) of
            Fun(fLabel, _) -> rememberFun(env, fLabel, args, body)
          esac
      | _                     -> env
    esac
  }, env, defs)
}

var sb = singletonBuffer;

-- Compiles an expression into a stack machine code.
-- Takes an expression, returns a list of stack machine
-- instructions.


-- Some guidelines for compiling functions and scopes.
--
-- 1. use env.beginScope/env.endScope properly;
-- 2. in each scope traverse the definitions twice:
--    a. first, add all bindings into symbolic state,
--       using end.addVar/env.addFun;
--    b. do not forget to generate GLOBAL instructions for
--       top-level variable declarations;
--    c. on the secord traversal add functions themselves for
--       future code generation (use env.rememberFun).
-- 3. the overall flow for codegeneration:
--    a. generate the code for thetopmost expression (the progrman itself);
--    b. take list of nested functions definitions via env.getFuns and
--       generate the code for each function;
--    c. repeat b. until no function definitions appear.
-- 4. when compiling functions, do not forget to create proper
--    environment (use env.beginFun) and register arguments
--    (use env.addArg).

fun addDefs(env, defs) {
  foldl(fun (env, def) {
    case def of
        Var(names)         -> addVars(env, names)
      | Fun(name, args, _) ->
        case genFunLabel(env, name) of
          [fLabel, env] -> addFun(env, name, fLabel, args.size)
        esac
    esac
  }, env, defs)
}

-- Compiles an expression into a stack machine code.
-- Takes an expression, returns a list of stack machine
-- instructions.
-- Compiles an expression into a stack machine code.
-- Takes an expression, returns a list of stack machine
-- instructions.
public fun compileSM (stmt) {
  fun label (lab, labUsed) {
    if labUsed
    then singletonBuffer (LABEL (lab))
    else emptyBuffer ()
    fi
  }

  fun compileFun(env, Fun (fLabel, args, body, state)) {
    case genLabel(addArgs(beginFun(env, state), args)) of
      [lab, env] ->
        case compile(lab, env, body) of
          [labUsed, env, code] ->
            [env, label(fLabel, true) <+> sb(BEGIN(fLabel, args.size, getLocals(env))) <+> code <+> label(lab, labUsed) <+> sb(END)]
        esac
    esac
  }

  fun compileFuns(env) {
    case getFuns(env) of
      [funs, env] ->
        case foldl(
          fun ([env, code], f) { case compileFun(env, f) of [env, funCode] -> [env, code <+> funCode] esac },
          [env, emptyBuffer()],
          funs
        ) of [env, code] ->
          case getFuns(env) of
              [{ }, _]   -> [env, code]
            | [_ : _, _] -> case compileFuns(env) of [env, newCode] -> [env, code <+> newCode] esac
          esac
        esac
    esac
  }

  fun compileList(env, stmts) {
    foldl(fun ([env, code], stmt) {
      case genLabel(env) of
        [lab, env] ->
          case compile(lab, env, stmt) of
            [labUsed, env, stmtCode] -> [env, code <+> stmtCode <+> label(lab, labUsed)]
          esac
      esac
    }, [env, emptyBuffer()], stmts)
  }
  
  fun compile (lab, env, stmt) {
    case stmt of
        Skip               -> [false, env, emptyBuffer()]
      | Var    (x)         -> [false, env, singletonBuffer(LD(env.lookupVar(x)))]
      | Ref    (x)         -> [false, env, singletonBuffer(LDA(env.lookupVar(x))) <+ LDA(env.lookupVar(x))]
      | Const  (n)         -> [false, env, singletonBuffer(CONST(n))]
      | Scope  (defs, exp) ->
        case beginScope(env) of
          env -> case addDefs(env, defs) of
            env -> case rememberFuns(env, defs) of
              env -> case compile(lab, env, exp) of
                [labUsed, env, code] -> [labUsed, endScope(env),
                  if isGlobal(env) then
                    foldl(
                      fun (code, def) { code <+ GLOBAL(def) },
                      emptyBuffer(),
                      flatten(map(fun (def) { case def of Var(names) -> names | _ -> { } esac }, defs))
                    )
                    else emptyBuffer()
                  fi <+> code
                ]
              esac
            esac
          esac
        esac
      | Call (name, args)  ->
        var func = lookupFun(env, name);
        var argsCode = compileList(env, args);
        case func of
          Fun(fLabel, nargs) ->
            if nargs != args.size then failure("Invalid number of arguments") fi;
            case argsCode of
              [env, argsCode] -> [
                false,
                env,
                if (fLabel[0] == '$') then argsCode <+ BUILTIN(fLabel, nargs) else argsCode <+ CALL(fLabel, nargs) fi
              ]
            esac
        esac
      | Binop (op, s1, s2) ->
        case env.genLabel of
          [opLab, env] ->
            case compile(opLab, env, Seq(s1, s2)) of
              [opLabUsed, env, opsCode] -> [false, env, opsCode <+> label(opLab, opLabUsed) <+ BINOP(op)]
            esac
        esac
      | Skip               -> [false, env, emptyBuffer()]
      | Read  (x)          -> [false, env, singletonBuffer(READ) <+ ST(env.lookupVar(x))]
      | Write (e)          ->
        case env.genLabel of
          [writeLab, env] ->
            case compile(writeLab, env, e) of
              [writeLabUsed, env, eCode] -> [false, env, eCode <+> label(writeLab, writeLabUsed) <+ WRITE]
            esac
        esac
      | Assn  (x, v)       ->
        case env.genLabel of
          [staLab, env] ->
            case compile(staLab, env, Seq(x, v)) of
              [staLabUsed, env, assnCode] -> [false, env, assnCode <+> label(staLab, staLabUsed) <+ STA]
            esac
        esac
      | Seq   (s1, s2)     ->
        case env.genLabel of
          [s2Lab, env] ->
            case compile(s2Lab, env, s1) of
              [s2LabUsed, env, s1Code] ->
                case compile(lab, env, s2) of
                  [labUsed, env, s2Code] -> [labUsed, env, s1Code <+> label(s2Lab, s2LabUsed) <+> s2Code]
                esac
            esac
        esac
      | If (e, s1, s2)     ->
        case env.genLabels(2) of
          [cjmpLabel, s1Lab, env] ->
            case compile(cjmpLabel, env, e) of
              [cjmpLabelUsed, env, eCode] ->
                case compile(lab, env, s1) of
                  [_, env, s1Code] ->
                    case compile(lab, env, s2) of
                      [_, env, s2Code] ->
                        [true, env, eCode <+> label(cjmpLabel, cjmpLabelUsed) <+> CJMP("ne", s1Lab).sb <+> s2Code <+> JMP(lab).sb <+> label(s1Lab, true) <+> s1Code]
                    esac
                esac
            esac
        esac
      | While (e, s)       ->
        case env.genLabels(3) of
          [cjmpLabel, eLab, sLab, env] ->
            case compile(cjmpLabel, env, e) of
              [cjmpLabelUsed, env, eCode] ->
                case compile(eLab, env, s) of
                  [_, env, sCode] ->
                    [false, env, JMP(eLab).sb <+> label(sLab, true) <+> sCode <+> label(eLab, true) <+> eCode <+> label(cjmpLabel, cjmpLabelUsed) <+> CJMP("ne", sLab).sb]
                esac
            esac
        esac
      | DoWhile (s, e)     ->
        case env.genLabels(3) of
          [cjmpLabel, eLab, sLab, env] ->
            case compile(cjmpLabel, env, e) of
              [cjmpLabelUsed, env, eCode] ->
                case compile(eLab, env, s) of
                  [eLabUsed, env, sCode] ->
                    [false, env, label(sLab, true) <+> sCode <+> label(eLab, eLabUsed) <+> eCode <+> label(cjmpLabel, cjmpLabelUsed) <+> CJMP("ne", sLab).sb]
                esac
            esac
        esac
      | Ignore(e)          ->
        case compile(lab, env, e) of
          [labUsed, env, eCode] -> [labUsed, env, eCode <+ DROP]
        esac
      | String(s)          -> [false, env, sb(STRING(s))]
      | Array(a)           -> case compileList(env, a) of [env, code] -> [false, env, code <+> sb(ARRAY(a.size))] esac
      | Elem(a, i)         ->
        case env.genLabel of
          [elemLab, env] ->
            case compile(elemLab, env, Seq(a, i)) of
              [elemLabUsed, env, code] -> [false, env, code <+> label(elemLab, elemLabUsed) <+ ELEM]
            esac
        esac
      | ElemRef(a, i)      ->
        case env.genLabel of
          [elemRefLab, env] ->
            case compile(elemRefLab, env, Seq(a, i)) of
              [elemRefLabUsed, env, code] -> [false, env, code <+> label(elemRefLab, elemRefLabUsed)]
            esac
        esac
      | Sexp(name, a)      -> case compileList(env, a) of [env, code] -> [false, env, code <+> sb(SEXP(name, a.size))] esac
      | _                  -> failure("compileSM not implemented for %s\n", stmt.string)
    esac
  }

  case initCompEnv ().beginScope.addFun ("read"  , "$read"  , 0)
                                .addFun ("write" , "$write" , 1)
                                .addFun ("length", "$length", 1).genLabel of
    [endLab, env] ->
      case compile(endLab, env, stmt) of
        [endLabUsed, env, code] -> (
          var mainCode = sb(LABEL("main")) <+ BEGIN("main", 0, env.getLocals) <+> code <+> label(endLab, endLabUsed) <+ END;
          case compileFuns(env) of
            [env, funsCode] -> getBuffer $ mainCode <+> funsCode
          esac
        )
      esac
  esac
}
