-- Expression evaluator

import List;
import Array;
import State;
import World;
import Lexer;
import Parser;
import Fun;
import Builtins;

-- As association map which maps "\otimes" into "\oplus"
var ops = {
  ["+" , infix + ],
  ["-" , infix - ],
  ["*" , infix * ],
  ["/" , infix / ],
  ["%" , infix % ],
  ["==", infix ==],
  ["!=", infix !=],
  ["<" , infix < ],
  ["<=", infix <=],
  [">" , infix > ],
  [">=", infix >=],
  ["&&", infix &&],
  ["!!", infix !!]
};

-- The evaluator for infix operators: takes an operator as a string
-- and two operand values, and returns the result
public fun evalOp (op, l, r) {
  case assoc (ops, op) of
    Some (f) -> f (l, r)
  esac
}

-- Evaluates an expression "expr" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A expression is represented by a data structure of the following shape:
--
-- expr = Assn    (expr, expr)         |
--        Set     (string, expr)       |
--        Seq     (expr, expr)         |
--        Skip                         |
--        if      (expr, expr, expr)   |
--        While   (expr, expr)         |
--        DoWhile (expr, expr)         |
--        Var     (string)             |
--        Ref     (string)             |
--        Const   (int)                |
--        Binop   (string, expr, expr) |
--        Call    (string, expr list)  |
--        Ignore  (expr)               |
--        String  (string)             |
--        Array   (expr list)          |
--        Elem    (expr, expr)         |
--        ElemRef (expr, expr)         |
--        Builtin (string, expr list)

-- Helper function: checks that given name designates a regular variable in
-- a given state
fun lookupVar (state, name) {
  case state.lookup (name) of
    x@Var (_) -> x
  | _         -> error (sprintf ("the name ""%s"" does not designate a variable", name), getLoc (name))
  esac
}

-- Helper function: checks that given name designates a function in
-- a given state
fun lookupFun (state, name) {
  case state.lookup (name) of
    x@Fun (_, _) -> x
  | _            -> error (sprintf ("the name ""%s"" does not designate a function", name), getLoc (name))
  esac
}

-- Helper function: adds a bunch of regular variables current scope
fun addNames (state, names) {
  foldl (fun (s, name) {s.addName (name, Val (0))}, state, names)
}

-- Helper function: adds a function in current scope
fun addFunction (state, name, args, body) {
  state.addName (name, Fun (args, body))
}

-- Helper function: adds function arguments in the current scope
fun addArgs(state, argNames, argVals) {
  foldl(fun (s, [n, v]) { addName(s, n, Var(v)) }, state, zip(argNames, argVals))
}

-- Adds variables and functions from defs to the state s
fun addDefs(state, defs) {
  foldl(fun (s, def) {
    case def of
        Var(names)            -> addNames(s, names)
      | Fun(name, args, body) -> addFunction(s, name, args, body)
    esac
  }, state, defs)
}
                                        
-- Evaluates a list of expressions, properly threading a configurations.
-- Returns the final configuration and the list of values
fun evalList (c, exprs) {
  case foldl (fun ([c, vals], e) {
                case eval (c, e) of
                  [c, v] -> [c, v : vals]
                esac
              },
              [c, {}],
              exprs) of
    [c, vals] -> [c, reverse (vals)]
  esac
}

fun eval (c@[s, w], expr) {
  case expr of
      Var(x)                -> [c, case lookupVar(s, x) of Var(x) -> x esac]
    | Const(x)              -> [c, x]
    | Ref(x)                -> [c, Ref(x)]
    | Binop (op, e1, e2)    -> case evalList(c, { e1, e2 }) of [c, { x, y }] -> [c, evalOp(op, x, y)] esac
    | Skip                  -> [c, None]
    | Assn(l, r)            ->
      case evalList(c, { l, r }) of
        [[s, w], { Ref(l), r }] -> [[s <- [l, Var(r)], w], r] |
        [[s, w], { ElemRef(a, i), r }] -> a[i] := r;[[s, w], r]
      esac
    | Read(x)               -> (
      var readRes = readWorld(w);
      [[s <- [x, Var(readRes.fst)], readRes.snd], None]
    )
    | Write(e)              -> case eval(c, e) of [[s, w], e] -> [[s, writeWorld(e, w)], None] esac
    | Seq(e1, e2)           -> case eval(c, e1) of [c, _] -> eval(c, e2) esac
    | If(e, e1, e2)         -> case eval(c, e) of [c, e] -> if e then eval(c, e1) else eval(c, e2) fi esac
    | loop@While(e, stmt)   -> case eval(c, e) of
      [c, e] -> if e
        then case eval(c, stmt) of [c, _] -> eval(c, loop) esac
        else [c, None]
      fi
    esac
    | loop@DoWhile(stmt, e) -> eval(c, Seq(stmt, While(e, stmt)))
    | Ignore(e)             -> case eval(c, e) of [c, e] -> [c, None] esac
    | Scope(defs, e)        ->
      case eval([addDefs(enterScope(s), defs), w], e) of
        [[s, w], e] -> [[leaveScope(s), w], e]
      esac
    | Call(name, args)      ->
      case lookupFun(s, name) of
        Fun(argNames, body) ->
          case evalList([s, w], args) of
            [[s, w], argVals] ->
              case body of
                External -> evalExternalFun([s, w], name, argVals) |
                _        ->
                case eval([addArgs(enterFunction(s), argNames, argVals), w], body) of
                  [[inner_s, w], e] -> [[s.leaveFunction(inner_s.getGlobal), w], e]
                esac
              esac
          esac
      esac
    | Array(elems) -> case evalList(c, elems) of [c, elems] -> [c, listArray(elems)] esac
    | String(s) -> [c, s]
    | Elem(a, i) -> case evalList(c, { a, i }) of [c, { a, i }] -> [c, a[i]] esac
    | ElemRef(a, i) -> case evalList(c, { a, i }) of [c, { a, i }] -> [c, ElemRef(a, i)] esac
    | Sexp(_, args) -> case evalList(c, args) of [c, elems] -> [c, listArray(elems)] esac
  esac
}

fun evalExternalFun(c@[s, w], name, args) {
  case name of
    "read"   -> case readWorld(w) of [res, wo] -> [[s, wo], res] esac |
    "write"  -> [[s, writeWorld(args[0], w)], None] |
    "length" -> [c, length(args[0])] |
    _        -> error(sprintf("the name ""%s"" does not designate to the external function", name), getLoc(name))
  esac
}


-- Evaluates a program with a given input and returns an output
public fun evalExpr (input, expr) {
  case eval ([emptyState ().enterScope.addName ("read",   Fun ({}, External))
                                      .addName ("write",  Fun ({"a"}, External))
                                      .addName ("length", Fun ({"a"}, External)), createWorld (input)], expr) of
    [c, _] -> c.snd.getOutput
  esac
}
